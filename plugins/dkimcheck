#! /usr/bin/perl -w

=head1 NAME

dkimcheck -- Check the DKIM / DomainKeys signatures in a message

=head1 DESCRIPTION

If an incoming message has a DKIM signature then this plugin will check
the validity of the message and report the results as a header in the 
mail message.

=head1 CONFIG

None needed right now

=head1 TODO

Verify that checks on DomainKeys are working

Determine if there is a way to individually check signatures.  The DKIM
library wants to return the best result of the available signatures.

Possibly put a timeout in there for the DNS lookups

Add in ability to reject messages that fail the check

=head1 REQUIREMENTS

This module requires the Mail::DKIM module found on CPAN here:

L<http://search.cpan.org/~jaslong/Mail-DKIM-0.26/lib/Mail/DKIM.pm>

=head1 AUTHOR

Written by Matthew Harrell <mharrell@bittwiddlers.com>.

=cut


use strict;
use Mail::DKIM;
use Mail::DKIM::Verifier;


sub register {
  my ($self, $qp, @args) = @_;
  %{$self->{_args}} = @args;
}

sub hook_data_post {
  my ($self, $transaction) = @_;

  $transaction->header->delete ( "X-DKIM-Authentication-Results" );
  # if this isn't signed, just move along
  #
  my $count = 0;
  $count += $transaction->header->count ( 'DKIM-Signature' );
  $count += $transaction->header->count ( 'DomainKey-Signature' );
  return DECLINED unless ( $count gt 0 );
	
  $self->log ( LOGNOTICE, "$count signatures found" );
  my $dkim = new Mail::DKIM::Verifier;
  return DECLINED unless ( $dkim );
  
  # take all the headers, reformat them to eliminate cr/lf and push into
  #  dkim.  dkim seems particular about the cr/lf
  #
  # my %hdrs = %{ $transaction->header->header_hashref() };

  # foreach my $key ( keys %hdrs ) {
  #   my $val = join ( "", @{$hdrs{$key}} );
  #   $val =~ s/[\n\r]//g;

    # $self->log ( LOGNOTICE, "Hdr: " . $key . ": " . $val );
  #   $dkim->PRINT ( $key . ": " . $val . "\x0D\x0A" );
  # }

  foreach my $line ( split ( /\n/s, $transaction->header->as_string ) ) {
    $line =~ s/\r?$/\015\012/s;
    $dkim->PRINT ( $line );
  }

  # push the body of the message on ensuring the cr/lf are correct
  #
  $transaction->body_resetpos;

  while ( my $line = $transaction->body_getline ) {
    $line =~ s/\r?\n$/\015\012/s;
    $dkim->PRINT ( $line );
  }

  $dkim->CLOSE;

  # get the key policy - need to act on this
  #
  my $policy = $dkim->fetch_author_policy;
  my $policy_result = $policy->apply ( $dkim );

  # print the result
  #
  foreach my $signature ($dkim->signatures) {
    $transaction->header->add ( "X-DKIM-Authentication",
				    "domain: " . $signature->domain .
				    ", selector: " . $signature->selector .
				    ", result: " . $signature->result_detail .
				    ", policy: " . $policy_result,
				 0 );

    $self->log ( LOGNOTICE, "domain: " . $signature->domain .
			    ", selector: " . $signature->selector .
			    ", result: " . $signature->result_detail .
			    ", policy: " . $policy_result );
  }

  #if ($policy_result eq "reject" and $self->{_args}{dkim_deny}) {
  #  return (DENY, "SPF forgery: $smtp_comment");
  #}

  # Should possibly also add an Authentication-Results header as in dkimproxy.
  # http://tools.ietf.org/id/draft-kucherawy-sender-auth-header-20.txt
  # and critiqued in http://tools.ietf.org/html/draft-otis-auth-header-appeal-00
  $transaction->header->add ( "X-DKIM-Authentication-Results",
				  $dkim->result_detail, 0 );

  return DECLINED;
}
